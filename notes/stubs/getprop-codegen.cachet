import "../cacheir.cachet";
import "../codegen.cachet";
import "../js.cachet";
import "../utils.cachet";

ir CacheStub emits CacheIR {
    op GetProp(value: Value, keyValue: Value) {
        CacheIR::initStub();
        let valueId = CacheIR::defineInputValueId();
        CacheIR::setValueOutput();

        if !PropertyKey::nameOrSymbolFromValue(keyValue, out let key, out let nameOrSymbol) {
            clearPendingException();
            return;
        }

        if Value::isObject(value) {
            let object = Value::toObject(value);
            emit CacheIR::GuardToObject(valueId);
            let objectId = OperandId::toObjectId(valueId);

            if nameOrSymbol {
                let kind = canAttachNativeGetProp(object, key, out let holder, out let propInfo);
                if kind == NativeGetPropKind::Slot {
                    let object = Object::toNativeObject(object);

                    let shape = Object::shapeOf(object);
                    let shapeField = CacheIR::writeShapeField(shape);
                    emit CacheIR::GuardShape(objectId, shapeField);

                    let holder = MaybeNativeObject::toNativeObject(holder);
                    let propInfo = MaybePropertyInfo::toPropertyInfo(propInfo);
                    let slot = PropertyInfo::slot(propInfo);

                    if object == holder {
                        if NativeObject::hasFixedSlot(object, slot) {
                            let offset = NativeObject::offsetOfFixedSlot(slot);
                            let offsetField = CacheIR::writeInt32Field(offset as Int32);
                            emit CacheIR::LoadFixedSlotResult(objectId, offsetField);
                        } else {
                            let offset = NativeObjectSlots::offsetOfSlot(
                                slot - NativeObject::numFixedSlots(object)
                            );
                            let offsetField = CacheIR::writeInt32Field(offset as Int32);
                            emit CacheIR::LoadDynamicSlotResult(objectId, offsetField);
                        }
                    }
                }
            }
        }

        emit CacheIR::ReturnFromIC();
    }
}
